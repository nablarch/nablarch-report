group = 'com.nablarch.report'
version = '1.1.0'
description = '帳票ライブラリ'

buildscript {
  repositories {
    mavenLocal()
    maven { url "${developLibUrl}" }
    jcenter()
  }
  
  dependencies {
    classpath "com.nablarch.dev:nablarch-gradle-plugin:${nablarchGradlePluginVersion}"
    classpath "net.saliman:gradle-cobertura-plugin:${coberturaPluginVersion}"
    classpath "io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE"
  }
}

ext.encoding = 'utf-8'

apply {
  plugin 'com.nablarch.dev.nablarch-sonarqube'
  plugin 'com.nablarch.dev.nablarch-build'
  plugin 'com.nablarch.dev.nablarch-maven-deploy'
  from 'https://github.com/nablarch/nablarch-gradle-plugin/blob/develop/scripts/cobertura/cobertura-0.0.1.gradle?raw=true'
  plugin "io.spring.dependency-management"
}

repositories {
  mavenLocal()
  maven { url "${developLibUrl}" }
  jcenter()
  
  // サードパーティ製ライブラリの格納リポジトリ
    if(project.hasProperty('nablarchExtRepoUrl')) {
       maven { url "${nablarchExtRepoUrl}" }
  }
}

dependencyManagement {
  imports {
    mavenBom 'com.nablarch.profile:nablarch-bom:5u9'
  }
  applyMavenExclusions false
}

[compileJava, compileTestJava].options*.encoding = encoding

configurations {
  all*.exclude module: 'nablarch-test-support-hereis'
  all*.exclude module: 'jsp-api-2.1-glassfish'
}

dependencies {
  compile 'commons-beanutils:commons-beanutils:1.8.0'
  compile 'net.sf.jasperreports:jasperreports:5.6.1'
  
  compile 'com.nablarch.framework:nablarch-core-repository'
  compile 'com.nablarch.framework:nablarch-common-idgenerator-jdbc'
  compile 'com.nablarch.framework:nablarch-fw'

  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'

  testRuntime 'com.lowagie:iTextAsian:1.0.0'

}

//------------------------------------------------------------------------------
// テスト実行用の設定
//------------------------------------------------------------------------------
test {
  ignoreFailures = true

  maxHeapSize = '64m'
  minHeapSize = '64m'

  include '**/*Test.class'

  testLogging {
    events "failed", "skipped", "passed"
    displayGranularity = 1
  }

  enableAssertions = false
  systemProperty 'java.security.egd', 'file:/dev/./urandom'

  jvmArgs '-XX:-UseSplitVerifier'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}

